;; Jetton Lockup Contract v1 by delpydoc@astralyx.dev

;; Storage TL-B Scheme;
;; storage#_ jetton_balances: (HasmapE 256 Coins) receiver_address:MsgIntAddress unlocked_at:uint64 owner_address:MsgIntAddress = Storage;

;; #include "imports/stdlib.fc";
#include "imports/constants.fc";

() save_data(cell jetton_balances, slice receiver_address, int unlocked_at, slice owner_address) impure inline {
    set_data(
        begin_cell()
            .store_dict(jetton_balances)
            .store_slice(receiver_address)
            .store_uint(unlocked_at, 64)
            .store_slice(owner_address)
            .end_cell()
    );
}

() recv_internal(int my_balance, int msg_value, cell in_msg_full, slice in_msg_body) impure {
    slice cs = in_msg_full.begin_parse();
    int flags = cs~load_uint(4);
    slice sender_address = cs~load_msg_addr();
    (int sender_wc, int sender_hashpart) = parse_std_addr(sender_address);
    throw_unless(333, sender_wc == workchain); ;; force chain

    if (flags & 1) { ;; bounced messages
        return ();
    }

    int op = in_msg_body~load_uint(32);
    int query_id = in_msg_body~load_uint(64);

    if (op == "lockup_data"c) { ;; drop stored lockup data
        send_raw_message(
            begin_cell()
                .store_uint(0x10, 6)
                .store_slice(sender_address)
                .store_coins(0)
                .store_uint(1, 1 + 4 + 4 + 64 + 32 + 1 + 1)
                .store_ref(
                    begin_cell()
                        .store_uint("lockup_data"c, 32)
                        .store_uint(query_id, 64)
                        .store_ref(get_data())
                        .end_cell()
                )
                .end_cell(),
            64
        );
        return ();
    }

    slice ds = get_data().begin_parse();
    (cell jetton_balances, slice receiver_address, int unlocked_at, slice owner_address) = (
        ds~load_dict(),
        ds~load_msg_addr(),
        ds~load_uint(64),
        ds~load_msg_addr()
    );

    if (op == op::transfer_notification) {
        if (jetton_balances.cell_depth() <= 10) { ;; dict overgas protection
            (slice vs, int found?) = jetton_balances.udict_get?(256, sender_hashpart);
            int stored_value = found? ? vs~load_coins() : 0;
            stored_value += in_msg_body~load_coins();

            ;; store amount of incoming tokens for processing by other smart contracts
            jetton_balances~udict_set_builder(
                256, sender_hashpart,
                begin_cell()
                    .store_coins(stored_value)
            );
            save_data(jetton_balances, receiver_address, unlocked_at, owner_address);
        }
        return ();
    }

    if (op == "edit_owner"c) {
        throw_unless(401, equal_slices(sender_address, owner_address));
        owner_address = in_msg_body~load_msg_addr();
        save_data(jetton_balances, receiver_address, unlocked_at, owner_address);
        return ();
    }
    if (op == "edit_receiver"c) {
        throw_unless(401, equal_slices(sender_address, receiver_address));
        receiver_address = in_msg_body~load_msg_addr();
        save_data(jetton_balances, receiver_address, unlocked_at, owner_address);
        return ();
    }
    if (op == "extend_lock"c) {
        throw_unless(401, equal_slices(sender_address, owner_address));
        int unlocked_at = max(in_msg_body~load_uint(64), unlocked_at);
        save_data(jetton_balances, receiver_address, unlocked_at, owner_address);
        return ();
    }
    if (op == op::excesses) { return (); }
    if (op == "withdraw"c) {
        throw_unless(401, equal_slices(sender_address, receiver_address));
        throw_unless(402, unlocked_at <= now());

        ;; if requested by recipient and lock time has expired, allow up to 4 internal messages to be sent
        while (in_msg_body.slice_refs()) {
            var msg = in_msg_body~load_ref().begin_parse();
            slice target_address = msg~load_msg_addr();
            (int target_wc, int target_hashpart) = parse_std_addr(target_address);
            int amount = msg~load_coins();
            cell msg_body = msg~load_uint(1) == 1 ? msg~load_ref() : null();
            (slice vs, int found?) = jetton_balances.udict_get?(256, target_hashpart);
            ;; check whether this transaction affects the stored token balances
            if (found? & (~ null?(msg_body))) {
                slice ms = msg_body.begin_parse();
                int op = ms~load_uint(32);
                if ((op == op::transfer) | (op == op::burn)) {
                    ;; if yes, update the onchain information about balances
                    ms~skip_bits(64);
                    int amount = ms~load_coins();
                    int stored_amount = vs~load_coins();
                    amount = max(stored_amount - amount, 0);

                    jetton_balances~udict_set_builder(
                        256, target_hashpart,
                        begin_cell()
                            .store_coins(amount)
                    );
                }
            }

            builder msg = begin_cell()
                .store_uint(0x18, 6) ;; bounce message
                .store_slice(target_address)
                .store_coins(amount)
                .store_uint(0, 1 + 4 + 4 + 64 + 32 + 1);

            if (~ null?(msg_body)) {
                msg = msg.store_uint(1, 1).store_ref(msg_body);
            } else {
                msg = msg.store_uint(0, 1);
            }

            send_raw_message(msg.end_cell(), 0);
        }

        save_data(jetton_balances, receiver_address, unlocked_at, owner_address);
        return ();
    }

    return ();

}

;; GET methods

(slice, slice, int, cell) lockup_data() method_id {
    slice ds = get_data().begin_parse();
    (cell jetton_balances, slice receiver_address, int unlocked_at, slice owner_address) = (
        ds~load_dict(),
        ds~load_msg_addr(),
        ds~load_uint(64),
        ds~load_msg_addr()
    );
    return (
        owner_address,
        receiver_address,
        unlocked_at,
        jetton_balances
    );
}
